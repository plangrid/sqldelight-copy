apply plugin: "com.vanniktech.maven.publish"
apply plugin: "org.jetbrains.dokka"

dokka {
  outputDirectory = "$rootDir/docs/1.x"
  outputFormat = 'gfm'
}

mavenPublish {
  releaseSigningEnabled = !getGpgKey().isEmpty()

  targets {
    installArchives {
      releaseRepositoryUrl = file("${rootProject.buildDir}/localMaven").toURI().toString()
      snapshotRepositoryUrl = file("${rootProject.buildDir}/localMaven").toURI().toString()
    }
    uploadArchives {
      releaseRepositoryUrl = System.getenv("ARTIFACTORY_PUBLISH_URL")
      repositoryUsername = System.getenv("ARTIFACTORY_USER")
      repositoryPassword = System.getenv("ARTIFACTORY_PASSWORD")
    }
  }
}

if (project.plugins.hasPlugin("org.jetbrains.kotlin.multiplatform")) {
  apply from: rootProject.file("gradle/gradle-mvn-mpp-push.gradle")
}

def getGpgKey() {
  return hasProperty('signingKey') ? signingKey : ""
}

signing {
  required { false }
  def signingKey = getGpgKey()
  if (!signingKey.isEmpty()) {
    useInMemoryPgpKeys(signingKey, "")
  }
}
