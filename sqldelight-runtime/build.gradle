apply plugin: 'org.jetbrains.kotlin.multiplatform'

archivesBaseName = 'sqldelight-runtime'

// TODO work around for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
  compileClasspath
}

kotlin {
  sourceSets {
    commonMain {
      dependencies {
        implementation deps.kotlin.stdlib.common
      }
    }
    commonTest {
      dependencies {
        implementation deps.kotlin.test.common
        implementation deps.kotlin.test.commonAnnotations
      }
    }
    jvmMain {
      dependencies {
        implementation deps.kotlin.stdlib.jdk
      }
    }
    jvmTest {
      dependencies {
        implementation deps.kotlin.test.junit
      }
    }
    jsMain {
      dependencies {
        implementation deps.kotlin.stdlib.js
      }
    }
    jsTest {
      dependencies {
        implementation deps.kotlin.test.js
      }
    }
    nativeMain {
      dependencies {
        implementation deps.stately
      }
    }
  }

  targets {

    if (getDeployHost() == "linux") {
      targetFromPreset(presets.jvm, 'jvm')
      targetFromPreset(presets.js, 'js') {
        tasks.getByName(compilations.main.compileKotlinTaskName).kotlinOptions {
          moduleKind = 'umd'
          sourceMap = true
          metaInfo = true
        }
      }
    }

    if (getDeployHost() == "macos") {
      targetFromPreset(presets.iosX64, 'iosX64')
      targetFromPreset(presets.iosArm32, 'iosArm32')
      targetFromPreset(presets.iosArm64, 'iosArm64')
    }
  }

  configure([targets.iosX64, targets.iosArm32, targets.iosArm64]) {
    compilations.main.source(sourceSets.nativeMain)
  }
}

afterEvaluate {
  // Alias the task names we use elsewhere to the new task names.
  tasks.create('install').dependsOn('publishKotlinMultiplatformPublicationToMavenLocal')
  tasks.create('installLocally') {
    dependsOn 'publishKotlinMultiplatformPublicationToTestRepository'
    dependsOn 'publishJvmPublicationToTestRepository'
    dependsOn 'publishJsPublicationToTestRepository'
    dependsOn 'publishMetadataPublicationToTestRepository'
  }
  tasks.create('installIosLocally') {
    dependsOn 'publishKotlinMultiplatformPublicationToTestRepository'
    dependsOn 'publishIosArm32PublicationToTestRepository'
    dependsOn 'publishIosArm64PublicationToTestRepository'
    dependsOn 'publishIosX64PublicationToTestRepository'
    dependsOn 'publishMetadataPublicationToTestRepository'
  }
  // NOTE: We do not alias uploadArchives because CI runs it on Linux and we only want to run it on Mac OS.
  //tasks.create('uploadArchives').dependsOn('publishKotlinMultiplatformPublicationToMavenRepository')
}

apply from: "$rootDir/gradle/gradle-mvn-mpp-push.gradle"

publishing {
  publications.all {
    println("Rewriting artifactId from: " + project.name)
    // Rewrite all artifacts from using the project name to just 'runtime'.
    artifactId = artifactId.replace(project.name, 'runtime')
  }
}